DOTFILES=$HOME/dotfiles

# Path to your oh-my-zsh installation.
export ZSH=$DOTFILES/zsh/oh-my-zsh

# === oh-my-zsh settings ===

ZSH_THEME="my-theme"
HYPHEN_INSENSITIVE="true"
COMPLETION_WAITING_DOTS="true"
ZSH_CUSTOM=$DOTFILES/zsh/oh_my_zsh_custom

# NOTE: PATH should be set before initializing oh-my-zsh plugins.
# Because we will choose plugins based on availability of commands.
export PATH="$HOME/bin:$DOTFILES/bin:/opt/homebrew/bin:$PATH"

# NOTE: This needs to be done before "source $ZSH/oh-my-zsh.sh",
# otherwise the "pyenv" plugin will print a warning.
if command -v pyenv 1>/dev/null 2>&1; then
    eval "$(pyenv init -)"
fi

plugins=(
    async
    colored-man-pages
    extract
    git
    git-prompt
    vi-mode
    zsh-syntax-highlighting
    zsh-autosuggestions
    zsh-autocomplete
)

if [[ `uname` == 'Darwin' ]]; then
    plugins+=(macos brew)
fi

# The following plugins will be loaded only when the corresponding commands exist.
typeset -A commands_to_plugins=(
    "eza" "eza"
    "bazel" "bazel"
    "docker" "docker"
    "mvn" "mvn"
    "pyenv" "pyenv"
    "zoxide" "zoxide"
)

for cmd in ${(@k)commands_to_plugins}; do
    if [[ $(command -v "$cmd") ]]; then
        plugins+=($commands_to_plugins[$cmd])
    fi
done

# Disable auto update for oh-my-zsh
zstyle ':omz:update' mode disabled

source $ZSH/oh-my-zsh.sh

# Support bash completion.
autoload bashcompinit
bashcompinit

# === Environment variables ===

export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

export XDG_CONFIG_HOME=~/.config

if which nvim >/dev/null 2>&1 ; then
    export EDITOR='nvim'
else
    export EDITOR='vim'
fi

HISTSIZE=130000
SAVEHIST=130000

# === fzf settings ===

if type fd >/dev/null 2>&1 ; then
    export FZF_DEFAULT_COMMAND="fd --hidden --strip-cwd-prefix --exclude .git"
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    export FZF_ALT_C_COMMAND="fd --type=d --hidden --strip-cwd-prefix --exclude .git"

    # Use fd for listing path candidates.
    _fzf_compgen_path() {
        fd --hidden --follow --exclude ".git" . "$1"
    }

    # Use fd to generate the list for directory completion
    _fzf_compgen_dir() {
        fd --type d --hidden --follow --exclude ".git" . "$1"
    }
fi
export FZF_FILE_PREVIEW_CMD="bat -n --color=always --line-range :$LINES {}"
export FZF_DIR_PREVIEW_CMD="ls -1 --color=always {}"
export FZF_PREVIEW_WINDOW="right,50%,<70(down,50%)"
export FZF_TMUX_OPTS='-p90%,90%'
FZF_DEFAULT_BIND="
ctrl-q:toggle-all,
ctrl-u:half-page-up,
ctrl-d:half-page-down,
ctrl-k:preview-up,
ctrl-j:preview-down,
ctrl-/:toggle-preview
"
FZF_DEFAULT_BIND="${FZF_DEFAULT_BIND//$'\n'/}"
export FZF_DEFAULT_OPTS="--reverse --bind '$FZF_DEFAULT_BIND'"
export FZF_CTRL_T_OPTS="
  --preview '[ -f {} ] && $FZF_FILE_PREVIEW_CMD || [ -d {} ] && $FZF_DIR_PREVIEW_CMD'
  --preview-window $FZF_PREVIEW_WINDOW"
export FZF_CTRL_R_OPTS="
  --preview 'echo {}' --preview-window up:3:hidden:wrap
  --bind 'ctrl-y:execute-silent(echo -n {2..} | pbcopy)+abort'
  --color header:italic
  --header 'CTRL-Y to copy command; CTRL-/ to preview full command'"

eval "$(fzf --zsh)"
# https://raw.githubusercontent.com/junegunn/fzf-git.sh/main/fzf-git.sh
source $DOTFILES/zsh/fzf-git.sh

# === Shell improvements ===

# Remove the extra space after the right prompt.
export ZLE_RPROMPT_INDENT=0

# Set cursor shape based on vi-mode.
VI_MODE_SET_CURSOR=true

# Make Ctrl-P/N search by prefix like Up/Down arrow keys do.
bindkey '^P' up-line-or-beginning-search
bindkey '^N' down-line-or-beginning-search

# Ctrl-V: edit in vim
bindkey '^V' edit-command-line
bindkey -M vicmd '^V' edit-command-line

# Expand ".../"
function expand-dots {
   if [[ $LBUFFER =~ ".*\.\.\.$" ]]; then
	 zle _expand_alias
	 zle expand-word
   fi
   zle self-insert
}
zle -N expand-dots
bindkey -M viins "/" expand-dots

# === Load aliases, functions, and local settings ===

source $DOTFILES/zsh/alias.zsh
source $DOTFILES/zsh/functions.zsh

if test -e "$DOTFILES/zsh/zshrc.local"; then
    source "$DOTFILES/zsh/zshrc.local"
fi

# === Misc ===

if type bat >/dev/null 2>&1 ; then
    export BAT_THEME="ansi"
fi
